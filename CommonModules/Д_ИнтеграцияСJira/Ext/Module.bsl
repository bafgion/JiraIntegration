Процедура ПриСозданииНаСервере(ЭтотОбъект) Экспорт
	
	Если НЕ УправлениеДоступом.ЕстьРоль("Д_РаботаСJira") Тогда
		Возврат;
	КонецЕсли;	
		
	Форма = ЭтотОбъект.ЭтаФорма;
	
	ПодменюКнопокФормы = Форма.Элементы.Добавить("ФормаМенюИнтеграцияJira", Тип("ГруппаФормы"), Форма.Элементы.ФормаКоманднаяПанель);
	ПодменюКнопокФормы.Вид = ВидГруппыФормы.Подменю;
	ПодменюКнопокФормы.Заголовок = "Интеграция Jira";
	ПодменюКнопокФормы.Картинка = БиблиотекаКартинок.Д_КартинкаJira;
	
	// Команды
	КомандаСоздатьШаблон = Форма.Команды.Добавить("Подключаемый_СоздатьШаблонИнтеграцииJira");
	КомандаСоздатьШаблон.Действие = "Подключаемый_ИнтеграцияJiraВыполнитьКоманду";
	
	КомандаНастроитьВыполнение = Форма.Команды.Добавить("Подключаемый_НастроитьВыполнениеЗапросов");
	КомандаНастроитьВыполнение.Действие = "Подключаемый_ИнтеграцияJiraВыполнитьКоманду";
	
	КомандаОткрытьСписокШаблонов = Форма.Команды.Добавить("Подключаемый_ОткрытьСписокШаблонов");
	КомандаОткрытьСписокШаблонов.Действие = "Подключаемый_ИнтеграцияJiraВыполнитьКоманду";
	
	КнопкаСоздатьШаблон = Форма.Элементы.Добавить("СоздатьШаблонИнтеграции", Тип("КнопкаФормы"), Форма.Элементы.ФормаМенюИнтеграцияJira);
	КнопкаСоздатьШаблон.Заголовок = "Создать шаблон интеграции";
	КнопкаСоздатьШаблон.ИмяКоманды = "Подключаемый_СоздатьШаблонИнтеграцииJira"; 
	
	// Кнопки
	КнопкаНастроитьВыполнение = Форма.Элементы.Добавить("НастроитьПорядок", Тип("КнопкаФормы"), Форма.Элементы.ФормаМенюИнтеграцияJira);
	КнопкаНастроитьВыполнение.Заголовок = "Настроить выполнение запросов";
	КнопкаНастроитьВыполнение.ИмяКоманды = "Подключаемый_НастроитьВыполнениеЗапросов";
	
	КнопкаОткрытьСписокШаблонов = Форма.Элементы.Добавить("ОткрытьСписокШаблонов", Тип("КнопкаФормы"), Форма.Элементы.ФормаМенюИнтеграцияJira);
	КнопкаОткрытьСписокШаблонов.Заголовок = "Открыть список шаблонов";
	КнопкаОткрытьСписокШаблонов.ИмяКоманды = "Подключаемый_ОткрытьСписокШаблонов";
	
КонецПроцедуры

Процедура ПослеЗаписиНаСервере(ЭтотОбъект) Экспорт
	
	ИдентификаторМетода = Перечисления.Д_ИдентификаторМетода.ПослеЗаписиНаСервере;
	ОбработкаЗапросов(ЭтотОбъект.Объект, ИдентификаторМетода);
	
КонецПроцедуры

Процедура ОбработкаЗапросов(Объект, ИдентификаторМетода)
	
	ОбъектСсылка = Объект.Ссылка;
	ТаблицаЗапросовПоОбъекту = Д_ИнтеграцияСJiraВызовСервера.НазначенныеТипыЗапросовПоОбъекту(ОбъектСсылка, ИдентификаторМетода);
	
	ТаблицаДанныхЗапросов = ИнициализацияТаблицы();
	
	Для Каждого СтрокаТаблицыЗапросов Из ТаблицаЗапросовПоОбъекту Цикл
		
		ТаблицаСопоставленныхДанных = Д_ИнтеграцияСJiraВызовСервера.ПолучитьСопоставленныеПараметры(СтрокаТаблицыЗапросов.Запрос);
		ШаблонЗапроса = Д_ИнтеграцияСJiraВызовСервера.ПолучитьЗапросДляОбъекта(СтрокаТаблицыЗапросов.Запрос);
		СопоставитьПоляЗапроса(ШаблонЗапроса, ТаблицаСопоставленныхДанных);
		СтруктураДанныхПолей = ПолучитьСтруктуруДанных(ТаблицаСопоставленныхДанных); 
		//                                                                                
		СтруктураЗаполненияТаблицы = Новый Структура("Идентификатор,ТипЗапроса,ТекстЗапроса,СтруктураДанных",
															СтрокаТаблицыЗапросов.ИдентификаторДляФормулы,
															ТипЗапроса(СтрокаТаблицыЗапросов.Запрос),
															ШаблонЗапроса, СтруктураДанныхПолей);
		//													
		НоваяСтрокаТаблицы = ТаблицаДанныхЗапросов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицы, СтруктураЗаполненияТаблицы);
		
	КонецЦикла; 
	
	ШаблонПорядкаВыполненияЗапросов = Д_ИнтеграцияСJiraВызовСервера.ПолучитьШаблонВыполненияЗапросов(ОбъектСсылка, ИдентификаторМетода);
	
	// TODO: Выполнение запроса к Jira/Insight после написания ядра для "общения" с Jira.
	
КонецПроцедуры

Функция ТипЗапроса(Запрос)
	Возврат Запрос.ТипЗапроса;	
КонецФункции

Функция ИнициализацияТаблицы()
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Идентификатор");
	ТаблицаДанных.Колонки.Добавить("ТипЗапроса");
	ТаблицаДанных.Колонки.Добавить("ТекстЗапроса");
	ТаблицаДанных.Колонки.Добавить("СтруктураДанных");
	
	Возврат ТаблицаДанных;
	
КонецФункции //ИнициализацияТаблицы

Функция ПолучитьСтруктуруДанных(ТаблицаСопоставленныхДанных)
	
	СтруктураДанных = Новый Структура();
	
	Для каждого Строка Из ТаблицаСопоставленныхДанных Цикл
		СтруктураДанных.Вставить(Строка.СопоставленноеЗначение, "");
	КонецЦикла;	                                                    
	
	Возврат СтруктураДанных;
	
КонецФункции

Процедура СопоставитьПоляЗапроса(ТекстЗапроса, ТаблицаСопоставленияПараметров)
	
	Для каждого СтрокаТаблицы Из ТаблицаСопоставленияПараметров Цикл
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, СтрокаТаблицы.Параметр, СтрокаТаблицы.СопоставленноеЗначение);
	КонецЦикла;
	
КонецПроцедуры
