#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Проверить доп сведения
	ПроверитьИлиСоздатьПредопределенныеЗначения();
	
	// Получить данные для формы
	ЗначенияРеквизитов = ПолучитьСтруктуруЗначений();
	СтруктураЗначений = Д_ОбщегоНазначения.ЗначенияПредопределенныхПараметров(ЗначенияРеквизитов);
	ЗаполнитьФормуПриСоздании(СтруктураЗначений);
	
	// Назначить видимость элементов
	УправлениеВидимостьюЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьInsightПриИзменении(Элемент)
	УправлениеВидимостьюЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура ОдинаковыеДанныеПриИзменении(Элемент)
	
	Insight_login = ?(ОдинаковыеДанные, Jira_login, "");
	Insight_password = ?(ОдинаковыеДанные, Jira_password, "");
	УправлениеВидимостьюЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура СохранитьДанные(Команда)
	СохранитьДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьПароль(Команда)
	
	Если Элементы.ОтображатьПароль.Пометка Тогда
		Элементы.Insight_password.РежимПароля = Истина;
		Элементы.Jira_password.РежимПароля = Истина;
		Элементы.ОтображатьПароль.Пометка = Ложь;
		Элементы.ОтображатьПароль.Картинка = БиблиотекаКартинок.ПоказатьПароль;
	Иначе
		Элементы.Insight_password.РежимПароля = Ложь;
		Элементы.Jira_password.РежимПароля = Ложь;
		Элементы.ОтображатьПароль.Пометка = Истина;
		Элементы.ОтображатьПароль.Картинка = БиблиотекаКартинок.СкрытьПароль;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьФормуПриСоздании(ЗначенияРеквизитов)
	
	Jira_URL = ЗначенияРеквизитов.Адрес;
	Jira_login = ЗначенияРеквизитов.Логин;
	Jira_password = ЗначенияРеквизитов.Пароль;
	ИспользоватьInsight = ЗначенияРеквизитов.ИспользоватьInsight;
	Insight_login = ЗначенияРеквизитов.ЛогинInsight;
	Insight_password = ЗначенияРеквизитов.ПарольInsight;
	ОдинаковыеДанные = ЗначенияРеквизитов.ОдинаковыеДанные;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруЗначений()
	
	ЗначенияРеквизитов = Новый Структура();
	ЗначенияРеквизитов.Вставить("ШаблонАдресаJira", "Адрес");
	ЗначенияРеквизитов.Вставить("ИспользуемыйЛогинJira", "Логин");
	ЗначенияРеквизитов.Вставить("ИспользуемыйПарольJira", "Пароль");
	ЗначенияРеквизитов.Вставить("ИспользуемыйЛогинInsight", "ЛогинInsight");
	ЗначенияРеквизитов.Вставить("ИспользуемыйПарольInsight", "ПарольInsight");
	ЗначенияРеквизитов.Вставить("ФлагИспользоватьInsight", "ИспользоватьInsight");
	ЗначенияРеквизитов.Вставить("ФлагОдинаковыеДанныеИнтеграцииJira", "ОдинаковыеДанные");
	
	Возврат ЗначенияРеквизитов;
	
КонецФункции

&НаСервере
Процедура ПроверитьИлиСоздатьПредопределенныеЗначения()
	
	ЗначенияРеквизитов = ПолучитьСтруктуруЗначений();
	РезультатПроверки = Д_ОбщегоНазначения.ЗначенияПредопределенныхПараметров(ЗначенияРеквизитов);
	
	Если РезультатПроверки.Адрес = Неопределено Тогда
		
		Для Каждого Элемент Из ЗначенияРеквизитов Цикл
			
			МенеджерЗаписи = РегистрыСведений.Д_НастройкиJira.СоздатьМенеджерЗаписи();
			
			МенеджерЗаписи.Ключ = Элемент.Ключ;
			МенеджерЗаписи.Значение = ?(Найти(Элемент.Ключ, "Флаг") = 0, "", Ложь);
			МенеджерЗаписи.Комментарий = "Создано системой автоматически для интеграции с Jira/Insight";
			
			МенеджерЗаписи.Записать();
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюЭлементов()
	
	Элементы.ГруппаInsight.Видимость = ?(ИспользоватьInsight, Истина, Ложь);
	Элементы.Insight_login.ТолькоПросмотр = ?(ОдинаковыеДанные, Истина, Ложь);
	Элементы.Insight_password.ТолькоПросмотр = ?(ОдинаковыеДанные, Истина, Ложь);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеНаСервере()
	
	Если ЭтаФорма.Модифицированность Тогда
		
		ЗначенияРеквизитов = Новый Структура();
		ЗначенияРеквизитов.Вставить("ШаблонАдресаJira", Jira_URL);
		ЗначенияРеквизитов.Вставить("ИспользуемыйЛогинJira", Jira_login);
		ЗначенияРеквизитов.Вставить("ИспользуемыйПарольJira", Jira_password);
		ЗначенияРеквизитов.Вставить("ИспользуемыйЛогинInsight", Insight_login);
		ЗначенияРеквизитов.Вставить("ИспользуемыйПарольInsight", Insight_password);
		ЗначенияРеквизитов.Вставить("ФлагИспользоватьInsight", ИспользоватьInsight);
		ЗначенияРеквизитов.Вставить("ФлагОдинаковыеДанныеИнтеграцииJira", ОдинаковыеДанные);
		
		Для каждого Элемент Из ЗначенияРеквизитов Цикл
			
			НаборЗаписей = РегистрыСведений.Д_НастройкиJira.СоздатьНаборЗаписей();		
			НаборЗаписей.Отбор.Ключ.Установить(Элемент.Ключ);
			НаборЗаписей.Прочитать();
			
			ПерезаписываемоеЗначение = НаборЗаписей[0];
			ПерезаписываемоеЗначение.Значение = Элемент.Значение;
			
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
		ЭтаФорма.Модифицированность = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти