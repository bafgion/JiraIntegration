
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СозданоИзФормыОбъекта") Тогда
		
		Если ЗаписьИмеетсяВРегистре() Тогда
			Элементы.ТипОбъекта.ТолькоПросмотр = Истина;
			Элементы.ИмяОбъекта.ТолькоПросмотр = Истина;
		КонецЕсли;
		
		Запись.ТипОбъекта = ?("Документ" = Параметры.ТипДокумента, 
		Перечисления.Д_ТипыОбъектов.Документ, Перечисления.Д_ТипыОбъектов.Справочник);
		Запись.ИмяОбъекта = Параметры.ИмяДокумента;
		Запись.UIDЗаписи = Новый УникальныйИдентификатор();
	КонецЕсли;
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизвольныйЗапросПриИзменении(Элемент)
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементов()
	
	Элементы.ГруппаШаблонЗапроса.Видимость = Запись.ПроизвольныйЗапрос;
	
	Если НЕ Запись.ПроизвольныйЗапрос Тогда
		Элементы.СопоставитьПараметры.Видимость = НЕ ПустаяСтрока(Запись.Шаблон);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонПриИзменении(Элемент)
	
	УстановитьВидимостьЭлементов();
	
	Если НЕ ПустаяСтрока(Запись.Шаблон) Тогда
		ДанныеОбъекта = Новый Структура();
		ДанныеОбъекта.Вставить("ИмяОбъекта", Запись.ИмяОбъекта);
		ДанныеОбъекта.Вставить("ТипОбъекта", Запись.ТипОбъекта);
		Запись.ШаблонЗапроса = Д_ИнтеграцияСJiraВызовСервера.СформироватьСтандартныйШаблонЗапроса(Запись.Шаблон, ДанныеОбъекта);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьПараметры(Команда)
	
	Если НЕ ЗаписьИмеетсяВРегистре() Тогда
		ПоказатьПредупреждение(, "Перед сопоставлением запишите данные!");
	Иначе
		СопоставлениеСсылка = ИмеетсяСопоставление();
		Если СопоставлениеСсылка = ПредопределенноеЗначение("Справочник.Д_СопоставлениеПолейЗапросов.ПустаяСсылка") Тогда
			
			ПараметрыОткрытия = Новый Структура();
			ПараметрыОткрытия.Вставить("UIDЗаписи", Запись.UIDЗаписи);
			ПараметрыОткрытия.Вставить("ТипОбъекта", Запись.ТипОбъекта);
			ПараметрыОткрытия.Вставить("ИмяОбъекта", Запись.ИмяОбъекта);
			ПараметрыОткрытия.Вставить("ШаблонJSON", Запись.Шаблон);
			ПараметрыОткрытия.Вставить("ТипЗапроса", Запись.ТипЗапроса);
			ПараметрыОткрытия.Вставить("ПроизвольныйЗапрос", Запись.ПроизвольныйЗапрос);
			
			ОткрытьФорму("Справочник.Д_СопоставлениеПолейЗапросов.Форма.ФормаЭлемента", ПараметрыОткрытия, 
							ЭтотОбъект,,,,,
							РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
		Иначе
			
			ПараметрыОткрытия = Новый Структура();
			ПараметрыОткрытия.Вставить("Ключ", СопоставлениеСсылка);
			ПараметрыОткрытия.Вставить("ПроизвольныйЗапрос", Запись.ПроизвольныйЗапрос);
			ПараметрыОткрытия.Вставить("ТипОбъекта", Запись.ТипОбъекта);
			ПараметрыОткрытия.Вставить("ИмяОбъекта", Запись.ИмяОбъекта);
			
			ОткрытьФорму("Справочник.Д_СопоставлениеПолейЗапросов.Форма.ФормаЭлемента", ПараметрыОткрытия, 
							ЭтотОбъект,,,,,
							РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ИмеетсяСопоставление()

	Возврат Справочники.Д_СопоставлениеПолейЗапросов.НайтиПоРеквизиту("GUIDЗаписи", Запись.UIDЗаписи);
	
КонецФункции

&НаСервере
Функция ЗаписьИмеетсяВРегистре()
	
	ЗаписьИмеется = Ложь;
	
	НаборЗаписей = РегистрыСведений.Д_НастройкиШаблоновОтправки.СоздатьНаборЗаписей();		
	НаборЗаписей.Отбор.UIDЗаписи.Установить(Запись.UIDЗаписи);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 1 Тогда
		ЗаписьИмеется = Истина;
	КонецЕсли;
	
	Возврат ЗаписьИмеется;
	
КонецФункции