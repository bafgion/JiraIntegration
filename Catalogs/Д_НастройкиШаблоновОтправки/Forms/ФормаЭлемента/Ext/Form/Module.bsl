
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СозданоИзФормыОбъекта") Тогда
		
		Элементы.ТипОбъекта.ТолькоПросмотр = Истина;
		Элементы.ИмяОбъекта.ТолькоПросмотр = Истина;
		
		Объект.ТипОбъекта = ?("Документ" = Параметры.ТипДокумента, 
		Перечисления.Д_ТипыОбъектов.Документ, Перечисления.Д_ТипыОбъектов.Справочник);
		
		Объект.ИмяОбъекта = Параметры.ИмяДокумента;
		
	КонецЕсли;
	
	ТипИИмяОбъекта = Строка(Объект.ТипОбъекта) + "." + Строка(Объект.ИмяОбъекта);
	ЗаполнитьСписокВыбораРеквизитов(ТипИИмяОбъекта);
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементов()
	
	Элементы.СопоставленныеПараметрыСопоставленноеЗначение.РежимВыбораИзСписка = Объект.ПроизвольныйЗапрос;
	Элементы.ГруппаШаблонЗапроса.Видимость = Объект.ПроизвольныйЗапрос;
		
	Элементы.ГруппаСопоставляемыеПараметры.Видимость = НЕ ПустаяСтрока(Объект.Шаблон);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораРеквизитов(ИмяОбъекта)
	
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяОбъекта);
	
	Для каждого Элемент Из ОбъектМетаданных.Реквизиты Цикл
		
		Элементы.СопоставленныеПараметрыСопоставленноеЗначение.СписокВыбора.Добавить(Элемент.Имя);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		НаименованиеСправочника = "СопоставлениеПолейЗапроса_" + Строка(Объект.ИмяОбъекта) + Строка(Объект.ТипЗапроса); 
		Объект.Наименование = НаименованиеСправочника;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПроизвольныйЗапросПриИзменении(Элемент)
	УстановитьВидимостьЭлементов();		
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВопросПоТаблице(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонJSON = ДополнительныеПараметры.ШаблонJSON;
	Объект.СопоставленныеПараметры.Очистить();
	
	КоличествоСтрок = Д_ИнтеграцияСJiraВызовСервера.ПолучитьКоличествоПараметровДляСопоставления(ШаблонJSON);
	
	Для Счетчик = 1 По КоличествоСтрок Цикл
		
		ИмяПараметра = "#Параметр" + Строка(Счетчик) + "#";
		
		НоваяСтрока = Объект.СопоставленныеПараметры.Добавить();
		НоваяСтрока.Параметр = ИмяПараметра;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонПриИзменении(Элемент)
	
	Если НЕ ПустаяСтрока(Объект.Шаблон) Тогда 
		
		ДополнительныеПараметры = Новый Структура("ШаблонJSON", Объект.Шаблон);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВопросПоТаблице", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, "Таблица параметров не пуста, желаете перезаполнить таблицу?",
									РежимДиалогаВопрос.ДаНет);
			
		// Заполняем текст запроса при условии, что не используется
		// произвольный запрос.
		Если НЕ Объект.ПроизвольныйЗапрос Тогда
			ДанныеОбъекта = Новый Структура();
			ДанныеОбъекта.Вставить("ИмяОбъекта", Объект.ИмяОбъекта);
			ДанныеОбъекта.Вставить("ТипОбъекта", Объект.ТипОбъекта);
			Объект.ШаблонЗапроса = Д_ИнтеграцияСJiraВызовСервера.СформироватьСтандартныйШаблонЗапроса(Объект.Шаблон, ДанныеОбъекта);
		КонецЕсли;
		
	КонецЕсли;    
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипЗапросаПриИзменении(Элемент)
	
	НаименованиеСправочника = "СопоставлениеПолейЗапроса" + Строка(Объект.ИмяОбъекта) + Строка(Объект.ТипЗапроса); 
	Объект.Наименование = НаименованиеСправочника;
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставленныеПараметрыСопоставленноеЗначениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СопоставленныеПараметры.ТекущиеДанные;
	
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.ИмяРеквизита) Тогда
		ТекущиеДанные.ИмяРеквизита = ТекущиеДанные.СопоставленноеЗначение;
	КонецЕсли;
	 
КонецПроцедуры
